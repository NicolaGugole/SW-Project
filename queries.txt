1.
prefix : <http://www.semanticweb.org/narrativeExam#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT ?realLocation (COUNT(?event) AS ?eventNumber)
WHERE{
  ?realLocation rdf:type :RealLocation .
  ?event rdf:type :Event ;
         :occursIn ?realLocation .
  }
GROUP BY ?realLocation

2.
prefix : <http://www.semanticweb.org/narrativeExam#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT ?book
WHERE{
  ?book rdf:type :Book .
  ?publisher rdf:type :Publisher ;
         :publishes ?book ;
         :hasID ?publisherId .
  FILTER(?publisherId < 5000)
  }

3.
prefix : <http://www.semanticweb.org/narrativeExam#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT ?eventWithNoHuman
WHERE{
  ?eventWithNoHuman rdf:type :Event .
  FILTER NOT EXISTS {
    ?character :isCharacterOf ?eventWithNoHuman ;
               rdf:type :Human .
  }
}

4.
prefix : <http://www.semanticweb.org/narrativeExam#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT ?title (COUNT(?narrative) AS ?numberOfNarratives) ?isbn ?publisher
WHERE{
  ?book rdf:type :Book;
        :hasISBN ?isbn;
        :hasTitle ?title;
        :hasPublisher ?publisher;
        :reports ?narrative .
  ?publisher rdf:type :Publisher .
  ?narrative rdf:type :Narrative .
}
GROUP BY ?book ?title ?isbn ?publisher

5.
prefix : <http://www.semanticweb.org/narrativeExam#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT DISTINCT ?event
WHERE{
  ?event rdf:type :Event .
  {
    ?human rdf:type :Human ;
          :isCharacterOf ?event .
  }
  UNION
  {
    ?realLocation rdf:type :RealLocation ;
                 :isPlaceOf ?event .
  }
}